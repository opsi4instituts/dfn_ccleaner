; ----------------------------------------------------------------
; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/
; ----------------------------------------------------------------

[Actions]
setloglevel = 6
;  Log Errors in Logfile but don't abort:
ExitOnError=false
; Show syntax errors in the script:
ScriptErrorMessages=on
; Dont trace step by step through the script:
TraceMode=off
; let started programs run in front of the winst window
StayOnTop=false

requiredWinstVersion >= "4.11.5.0"

DefVar $ProductId$ 
DefVar $Setupfile$ 
DefVar $InstallDir$
DefVar $PathToExe$
DefVar $DesktopIconName$
DefVar $Property_DesktopIcon$
DefVar $MinimumSpace$
DefVar $ExitCode$
DefVar $ErrorMsg$
DefVar $LicenseRequired$
DefVar $LicenseKey$
DefVar $LicensePool$
DefVar $NsisInstallBatch$
DefVar $LogDir$
DefVar $PrettyName$
DefVar $PackageVersion$
DefVar $Version$
DefVar $TaskName$
DefVar $CustomPostInstall$
DefVar $INST_SystemType$
DefStringList $TaskNameList$

Set $LogDir$ = "%SystemDrive%\tmp"

include_append "%ScriptPath%\check_nsis_exitcode.opsiscript"
include_append "%ScriptPath%\dfn_lib.opsiscript"

; ----------------------------------------------------------------
Set $ProductId$            = "ccleaner"
Set $LicenseRequired$      = "false"
Set $LicensePool$          = "p_" + $ProductId$
Set $MinimumSpace$         = "38 MB"
Set $PrettyName$			     = "CCleaner"
Set $PackageVersion$	     = takeString(1, splitString ("%installingProdVersion%","-"))
Set $Version$	             = strPart("%installingProdVersion%", "1", calculate(strPos("%installingProdVersion%","-") + "-" + "1" ))
Set $Setupfile$            = "ccsetup" + strPart($Version$, "1", "1") + strPart($Version$, "3", "2") + ".exe"
Set $DesktopIconName$      = "CCleaner"
Set $InstallDir$           = "%ProgramFiles32Dir%\CCleaner"
Set $Property_DesktopIcon$ = GetProductProperty("desktopicon","True")
Set $INST_SystemType$      = GetSystemType
Set $TaskNameList$				 = addtolist($TaskNameList$	, "CCleaner.exe")
Set $TaskNameList$				 = addtolist($TaskNameList$	, "CCleaner64.exe")

if ($INST_SystemType$ = "64 Bit System")
  Set $PathToExe$          = $InstallDir$ + "\CCleaner64.exe"
else 
  Set $PathToExe$          = $InstallDir$ + "\CCleaner.exe"
endif
; ----------------------------------------------------------------

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
   LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
   isFatalError "No Space"
   ; Stop process and set installation status to failed
else
   comment "Show product picture"
   ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $PrettyName$ + " " + $Version$

   if FileExists("%ScriptPath%\delsub.opsiscript")
      comment "Start uninstall sub section"
      Sub "%ScriptPath%\delsub.opsiscript"
   endif
	 
	 for %task% in $TaskNameList$ do sub_killtask
   
   Message "Installing " + $ProductId$ + " ..."
   
   if $LicenseRequired$ = "true"
      comment "Licensing required, reserve license and get license key"
      Sub_get_licensekey
   endif

  Set $NsisInstallBatch$ = "%ScriptPath%\"+ $Setupfile$ +" /S" + ' /D="' + $InstallDir$ + '"'

   comment "Start setup program"
   ChangeDirectory "%SCRIPTPATH%"
   ;----------------------------------------------
   Winbatch_install_nsis
   ;----------------------------------------------
   Sub_check_exitcode	
  
   if $Property_DesktopIcon$ = "True"
     LinkFolder_create
   else
     LinkFolder_delete
   endif 

   comment "include custom post install file"
   set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
   if not ($CustomPostInstall$ = "none")
	   if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
		   include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
	   endif
   endif

endif

; ----------------------------------------------------------------

[sub_killtask]
Set $TaskName$ = "%task%"
Killtask $TaskName$

; ----------------------------------------------------------------
; install section
; ----------------------------------------------------------------
[Winbatch_install_nsis]
;NSIS Nullsoft Scriptable Install System 
$NsisInstallBatch$



